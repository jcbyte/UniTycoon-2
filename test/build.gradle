apply plugin: 'application'

// Added jacoco plugin to produce test coverage report (for CI)
apply plugin: 'jacoco'

java.sourceCompatibility = 17
java.targetCompatibility = 17
if (JavaVersion.current().isJava9Compatible()) {
  compileJava.options.release.set(17)
}


// We need to add assets as a source directory during testing, similarly to the lwjgl3 project.
sourceSets.main.resources.srcDirs += [ rootProject.file('assets').path ]
sourceSets.test.resources.srcDirs += [ rootProject.file('assets').path ]

mainClassName = 'com.vikingz.unitycoon.test.HeadlessLauncher'
application.setMainClass(mainClassName)
eclipse.project.name = appName + '-test'

dependencies {
  implementation "com.badlogicgames.gdx:gdx-backend-headless:$gdxVersion"
  implementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
  implementation project(':core')

  // Add JUnit 5 and Mockito to the dependencies
  testImplementation platform('org.junit:junit-bom:5.10.0')
  testImplementation 'org.junit.jupiter:junit-jupiter'
  testImplementation 'org.mockito:mockito-core:5.11.0'
}


jar {
  archiveBaseName.set(appName)
  duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
  dependsOn configurations.runtimeClasspath
  from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
  manifest {
    attributes 'Main-Class': project.mainClassName
  }
  doLast {
    file(archiveFile).setExecutable(true, false)
  }
}

// Equivalent to the jar task; here for compatibility with gdx-setup.
tasks.register('dist') {
  dependsOn 'jar'
}



// Run JUnit when we want to test
test {
  useJUnitPlatform()
}

// Generate coverage report
jacocoTestReport {
  // Ensure tests have run before generating the report
  dependsOn tasks.test

  // Tell JaCoCo to look at core sources
  sourceSets project(':core').sourceSets.main

  reports {
    xml.required = false
    html.required = true
    csv.required = false
  }
}
